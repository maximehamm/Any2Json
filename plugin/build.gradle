plugins {
    id 'org.jetbrains.intellij'
    id 'org.jetbrains.kotlin.jvm' version '1.4.30'
}

group 'io.nimbly.json'

dependencies {

    implementation project(':common')

    runtimeOnly project(':extensions:java')
    runtimeOnly project(':extensions:kotlin')
    runtimeOnly project(':extensions:typescript')
    runtimeOnly project(':extensions:php')
    runtimeOnly project(':extensions:database')

    // Json
    implementation 'org.json:json:20180813'

    // CSV supports
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.11.2'

    // YAML supports
    implementation 'org.yaml:snakeyaml:1.27'
}

compileKotlin { kotlinOptions.jvmTarget = "11" }
compileTestKotlin { kotlinOptions.jvmTarget = "11" }

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version 'IU-203.7148.57'
    plugins = ['com.jetbrains.php:203.7148.15',
               'PsiViewer:203-SNAPSHOT']

    // intellij types :
    // 'IC' - IntelliJ IDEA Community Edition.
    // 'IU' - IntelliJ IDEA Ultimate Edition.
    // 'CL' - CLion.
    // 'PY' - PyCharm Professional Edition.
    // 'PC' - PyCharm Community Edition.
    // 'RD' - Rider.
    // 'GO' - GoLand.
    // 'JPS' - JPS-only
}

tasks.runPluginVerifier {
    ideVersions("IC-203.7148.40, CL-211.5787.12, PS-211.5787.18") //IC-203.7148.40, IU-203.7148.40, CL-211.5787.12, PS-211.5787.18
}

patchPluginXml {
    changeNotes """
      <b>Please Rate and Review this plugin !</b><br/><br/>
      Change notes :
      <ul>
        <li><b>2.6</b> Prettify Json literal inside PHP Editor</li>
        <li><b>2.5</b> Prettify Json literal inside Kotlin Editor</li>
        <li><b>2.4</b> Prettify Json literal inside Java Editor</li>
        <li><b>2.3</b> Supports using from Database table results</li>
        <li><b>2.2</b> Supports using from Database view</li>
        <li><b>2.1</b> Supports using from Debugger with Angular</li>
        <li><b>2.O</b> Supports of quite all IDEA products</li>
        <li><b>1.9</b> Supports of TypeScript</li>
        <li><b>1.8</b> Supports of Java Properties</li>
        <li><b>1.7</b> Supports of YAML</li>
        <li><b>1.6</b> Supports of CSV</li>
        <li><b>1.5</b> Supports of XML</li>
        <li><b>1.4</b> Supports using from debugger</li>
        <li><b>1.3</b> Mixing Kotlin and Java references</li>
        <li><b>1.2</b> Supports of Kotlin</li>
        <li><b>1.1</b> Supports of Java</li>
        <li><b>1.0</b> Initial version</li>
      </ul>
      <br/>
      Road map 
      <ul>
        <li><b>1.X</b> Supports of other languages !</li>
      </ul>
      """
}

tasks.buildSearchableOptions {
    enabled = false
}

jar {
    archiveBaseName = 'any2json'
}

publishPlugin {
    token = System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken")
}

